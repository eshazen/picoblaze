;;;
;;; demo Picoblaze program
;;; command parser with hex argument decode
;;; commands:
;;;   S [<start> [<count>]]     - dump scratchpad
;;;   p [<start> [<count>]]     - dump program
;;;   e <addr> <data>           - edit program
;;;   u <uue>                   - decode 3 char uue string for testing
;;;   b                         - start bootloader

;;; I/O port addresses
	
;;; UART ports (not used here but included for completeness)

;;; input ports
        CONSTANT UART_status_port, 00             ; Read status	(n/a this file)
        CONSTANT UART_RX6_input_port, 01	  ; rx data (n/a this file)
	CONSTANT clock_freq_in_mhz, 02		  ; system clock frequency in MHz
	CONSTANT get_peek_data_low, 03 ; program data low bits
	CONSTANT get_peek_data_mid, 04 ; program data middle bits
	CONSTANT get_peek_data_high, 05 ; ; program data upper bits

;;; output ports
	
	CONSTANT UART_TX6_output_port, 01	  ; tx data (n/a this file)
        CONSTANT reset_UART_port, 01		  ; K port to reset UART (n/a this file)
	
        CONSTANT set_baud_rate_lsb, 02 ; baud rate divisor, low byte
	CONSTANT set_baud_rate_msb, 04 ; baud rate divisor, high byte
	CONSTANT set_peek_addr_lsb, 06	; program RAM address low bits
	CONSTANT set_peek_addr_msb, 08  ; program RAM address high bits
	CONSTANT set_poke_data_low, 0a  ; program RAM write data latch lsb
	CONSTANT set_poke_data_mid, 0c  ; program RAM write data latch lsb
	CONSTANT set_poke_data_high, 0e  ; program RAM write data latch msb (triggers write)

;;; scratchpad
	CONSTANT input_pointer, 00 ; buffer count
	CONSTANT input_buffer, 01 ; input buffer
	CONSTANT input_last, 1f	  ; end of buffer

	CONSTANT max_tokens, 06	; number of tokens
	CONSTANT argc, 20	; token count
	CONSTANT argv, 21	; list of token offsets

	CONSTANT hexargs, 30	; hex arguments, 4 bytes each
	CONSTANT hexarg0, 30
	CONSTANT hexarg1, 34
	CONSTANT hexarg1_1, 35
	CONSTANT hexarg2, 38
	CONSTANT hexarg2_1, 39
	CONSTANT hexarg2_2, 3a
	CONSTANT hexarg3, 3c
	CONSTANT hexarg4, 40
	CONSTANT hexarg5, 44
	

;;;-------- code starts here -------

cold_start: 
	;; set 9600 baud (651 decimal)
	load s0, 139'd
        output s0, set_baud_rate_lsb
	load S0, 02
        output s0, set_baud_rate_msb	
        CALL reset_UART_macros
        CALL welcome_message

main:	load s5, ">"
	call UART_TX
	load s8, input_buffer	;set pointer to buffer start
	
m_inch:	call UART_RX_wait	;get char
	compare s5, 08		;backspace
	jump z, m_back
	compare s5, 0d		;CR?
	jump z, m_done
	compare s5, 20		;other control?
	jump c, m_inch		;yes, ignore

	compare s8, input_last	;end of buffer
	jump z, m_inch

	call UART_TX		;echo it
	store s5, (s8)		;store in buffer
	add s8, 01
	jump m_inch

m_back:	compare s8, input_buffer ; at start of buffer?
	jump z, m_inch		 ; yes, ignore BS
	sub s8, 01		 ; else move back
	call UART_TX		 ; echo BS
	jump m_inch

m_done:	call send_CR
	load s0, 00
	store s0, (s8)		; null-terminate string
	store s8, input_pointer
	
;;; 
;;; parse tokens until NUL seen
;;; 
	load s8, input_buffer	; parse here
	load s9, 00		; count arguments
	load sA, argv		; store argument pointers here

;;; if space, skip over it quietly
;;; if printable, mark as start of token
;;; if NUL, quit now
ftok:	
	fetch s0, (s8)		; get a character
	compare s0, 00		; Null?
	jump z, parse_end
	compare s0, 20		; is it a space?
	jump nz, nosp		; nope, start of token
	add s8, 01		; advance pointer past space
	jump ftok		; keep looking
;;; not a space, mark start of token and increment count
nosp:	add s9, 01		; increment argc
	store s8, (sA)		; store argv
	add sA, 01		; increment argv pointer
	compare s9, max_tokens	; max token count reached?
	jump z, parse_end
;;; now loop looking for either NUL or space
etok:	fetch s0, (s8)		; get a character
	compare s0, 00
	jump z, parse_end
	add s8, 01		; advance pointer
	compare s0, 20		; space?
	jump nz, etok		; no, keep looking
	jump ftok		; space, go look for next token

parse_end:
	store s9, argc

;;;
;;; convert tokens to hex
;;;
	fetch s9, argc		; count arguments
	load sA, argv		; argument pointers here
	load sB, hexargs

hxtok:	compare s9, 00		; all done?
	jump z, hx_end
	fetch s8, (sA)		; get argv[]
;;; zero our accumulator (s3...s0)
	xor s0, s0
	xor s1, s1
	xor s2, s2
	xor s3, s3
;;; parse hex characters one at a time to end of argument
hxchar:	fetch s5, (s8)		; get a character
	add s8, 01		; advance pointer
	compare s5, 20		; check for space/NUL
	jump z, hxtokend	; end of token
	compare s5, 00
	jump z, hxtokend
	call ascii_to_hex
	jump c, is_hex		; yup, it's hex
	;; not hex, ignore it
	jump hxchar
;;; it's hex
is_hex:		 ; shift entire arg left 4 bits and merge in new digit
	sl0 s0
	sla s1
	sla s2
	sla s3
	sl0 s0
	sla s1
	sla s2
	sla s3
	sl0 s0
	sla s1
	sla s2
	sla s3
	sl0 s0
	sla s1
	sla s2
	sla s3
	or s0, s5
	jump hxchar

;;; end of token (space, NUL)
hxtokend:	
	;; store accumulator
	store s0, (sB)
	add sB, 01
	store s1, (sB)
	add sB, 01
	store s2, (sB)
	add sB, 01
	store s3, (sB)
	add sB, 01
	
	add sA, 01		;get next argv
	sub s9, 01
	jump hxtok

hx_end:	
	
;;; 
;;; run command
;;; initialize s0 to argc
;;; 
	fetch s5, input_buffer
	and s5,5f		;convert command byte only to uppercase
	fetch s0, argc
	compare s5, "P"		;command P - dump program
	jump z, cmd_P
	compare s5, "S"		;command S - dump scratchpad
	jump z, cmd_S
	compare s5, "E"         ;command E - edit program
	jump z, cmd_E
	compare s5, "B"		;command B - UUencoded bootloader
	jump z, cmd_B
	compare s5, "U"		;command U - test UUE
	jump z, cmd_U
	compare s5, "W"		;command W - single UUE
	jump z, cmd_W
	compare s5, "J"		;command J - jump to address
	jump z, cmd_J
	jump main

;;; command error
err:	LOAD sB, error_msg'upper
	LOAD sA, error_msg'lower
	CALL send_message
	jump main

;;; jump
cmd_J:	fetch s6, hexarg1	; get address lsb
	fetch s7, hexarg1_1	; get address msb
	jump@ (s7, s6)

;;; test UUdecode one byte
cmd_W:	call one_uue
	jump nz, err
	call send_hex_byte
	call send_CR
	jump main

;;; test UU decode
cmd_U:	call read_uue		; 18 bits to s6, s7, s8
	load s4, s8
	call send_hex_byte
	load s4, s7
	call send_hex_byte
	load s4, s6
	call send_hex_byte
	call send_CR
	jump main
	
	

;;; bootloader, expect a file with the following:
;
; +zzz    specify load address for subsequent data
; =zzz    specify data to load starting at address
;         (usually repeated)
; $zzz    jump to specified address (marks end of file)
;
cmd_B:	call UART_RX_wait
	load s6, s5
;	load s4, s5
;	call send_hex_byte
;	call send_CR
	compare s6, "+"
	jump z, boot_addr
	compare s6, "="
	jump z, boot_data
	compare s6, "$"
	jump nz, err

;;; get address, just echo for now
	call read_uue		; get data to s6, s7, s8
	jump nz, err
	load s4, s7
	call send_hex_byte
	load s4, s6
	call send_hex_byte
	call send_CR
	jump main

;;; get address into registers sA, sB
boot_addr:
	call read_uue
	jump nz, err
	load sA, s6
	load sB, s7
	jump cmd_B

;;; get data, store in program RAM
boot_data:
	call read_uue
	jump nz, err
	output sA, set_peek_addr_lsb
	output sB, set_peek_addr_msb
	output s6, set_poke_data_low
	output s7, set_poke_data_mid
	output s8, set_poke_data_high
	;; increment address in sA, sB
	add sA, 01
	addcy sB, 00
	jump cmd_B

;;; read 3 bytes from UART followed by <CR>
;;; validate as UUE characters in range 20-5F
;;; extract 6 data bits from each, form 18 bit word (MSB received first)
;;; return 18 bits in s6 (low 8 bits), s7 (mid 8 bits) and s8 (high 2 bits)
;;; NZ on error
read_uue:
	call one_uue		; get one UUE to s4 (upper 2 bits)
	return nz
	load s8, s4		; copy to s8
	sr0 s8			; shift data (17,16) over to (1,0)
	sr0 s8
	sr0 s8
	sr0 s8			; done with s8
	load s7, s4
	sl0 s7			; shift 4 bits over to place in s7
	sl0 s7
	sl0 s7
	sl0 s7
	;; done with first byte
	call one_uue
	return nz
	load s5, s4		; copy to s5
	sr0 s5			; bits 2-5 of 2nd byte go to bits 0-3 of s7
	sr0 s5
	or s7, s5		; now s7 is good
	rr s4			; bits (1,0) of 2nd byte to to bits (7,6) of s6
	rr s4
	load s6, 00
	or s6, s4
	and s6, c0
	;; done with second byte
	call one_uue
	return nz
	or s6, s4
	;; done with third byte
	call UART_RX_wait
	;; FIXME:  should check for CR here
	xor s0, s0		; force Z set
	return

;;; receive one UUE character
;;; Z set if valid; NZ on error
one_uue:
	call UART_RX_wait	; receive character to s5
	sub s5, 20		; must be in range 20-5f
	return c		; will be nz as well, flagging error
	compare s5, 40
	return nc
	and s5, 3f
	load s4, s5
	xor s0, s0		; force Z set
	return
	


;;; edit one word of program RAM
cmd_E:	compare s0, 03
	jump nz, err
	fetch s6, hexarg1	; get address lsb
	fetch s7, hexarg1_1	; get address msb
	OUTPUT s6, set_peek_addr_lsb
	OUTPUT s7, set_peek_addr_msb

	fetch s6, hexarg2	; get data lsb
	OUTPUT s6, set_poke_data_low
	fetch s6, hexarg2_1
	OUTPUT s6, set_poke_data_mid
	fetch s6, hexarg2_2
	OUTPUT s6, set_poke_data_high
	jump main

;;; dump scratchpad
cmd_S:	load s6, 00		;default address
	load s8, 3f		;default count
	compare s0, 02		; address specified?
	jump c, sp_go
	fetch s6, hexarg1
	compare s0, 03		; count specified?
	jump c, sp_go
	fetch s8, hexarg2
	sub s8, 01
	
sp_go:	
	call sp_dump
	call send_CR
	jump main

;;; dump program ROM
cmd_P:				;defaults:
	LOAD s6, 00		; address low
	LOAD s7, 00		; address high
	LOAD s8, 10		; word count

	compare s0, 02		; address specified?
	jump c, dp_go		; nope
	fetch s6, hexarg1	; get address lsb
	fetch s7, hexarg1_1	; get address msb
	compare s0, 03		;count specified?
	jump c,dp_go
	fetch s8,hexarg2	;get count

dp_go:	
	call pr_dump
	jump main

;;;
;;; dump program RAM to terminal
;;; (s7,s6) = address  s8 = word count
;;; 
pr_dump:
	OUTPUT s6, set_peek_addr_lsb
	OUTPUT s7, set_peek_addr_msb

	LOAD s4, s7		; display address
	CALL send_hex_byte
	LOAD s4, s6
	CALL send_hex_byte
	CALL send_space		; then space

	INPUT s4, get_peek_data_high ;display 3 bytes of data
	CALL send_hex_byte
	INPUT s4, get_peek_data_mid
	CALL send_hex_byte
	INPUT s4, get_peek_data_low
	CALL send_hex_byte
	CALL send_CR

	ADD s6, 01		;increment address low
	ADDCY s7, 00		;increment address hight
;	ADDCY s8, 00            ; what's this?

	SUB s8, 01		;loop over word count
	JUMP C, main
	JUMP pr_dump

;;;
;;; dump scratchpad to terminal
;;; s6 = address  s8 = byte count
;;;
sp_dump:
	load s4, s6
	call send_hex_byte	;display address
	call send_space
	call send_space
sp_db:	fetch s4, (s6)
	call send_hex_byte
	call send_space
sp_nocr:
	add s6, 01		;increment address
	sub s8, 01
	return c
	;; end of line?
	test s6, 0f		;16 bytes done?
	jump nz, sp_db
	call send_CR
	jump sp_dump
	

welcome_message:
	LOAD sB, welcome_msg'upper
	LOAD sA, welcome_msg'lower
	CALL send_message
	HWBUILD s5                                ;hardware version defines ASCII letter
	CALL UART_TX
	RETURN 

welcome_msg:
	LOAD&RETURN s5, welcome$
	LOAD&RETURN s5, CR
	LOAD&RETURN s5, NUL

error_msg:
	LOAD&RETURN s5, error$
	LOAD&RETURN s5, CR
	LOAD&RETURN s5, NUL
	

STRING welcome$, "Monitor V1.4"	
STRING error$, "Error"	

        INCLUDE "uart_interface_routines.psm"
	INCLUDE "uart_utility.psm"
	
